buildscript {
    ext.kotlin_version = '1.4.32'
    ext.dokka_version = '1.4.30'

    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.14.2'
    }
}

plugins {
    id 'org.jlleitschuh.gradle.ktlint' version '10.0.0'
    id "com.github.ben-manes.versions" version "0.38.0"
}

ktlint {
    version = '0.39.0'
}

allprojects {
    group = GROUP
    version = VERSION_NAME

    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

subprojects { project ->
    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }
}

allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
        task.kotlinOptions {
            freeCompilerArgs += [
                    '-Xopt-in=kotlin.RequiresOptIn',
            ]
        }
    }
}

apply from: rootProject.file('dependencies.gradle')
